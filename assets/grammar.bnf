<start> ::= (<func>)* <main> (<func>)* <END>

<func> ::= "алё" <NAME> ":-)" <vars>? ";-)" <body>
<main> ::= "привет_масик" <body>
<vars> ::= <NAME> ("ещё" <NAME>)*

<statement> ::= <if> | <while> 
             | ((<declaration> | <assignment> | <ret>)? ("пж-пж")+)

<assignment> ::=  <NAME> 
        ("всего_за" 
        |       "подороже" |       "подешевле" 
        | "очень_подороже" | "очень_подешевле" 
        |       "подороже_что_пиздец"
        ) 
        <expr> 

<expr> ::= <compare_eq>
<compare_eq> ::= <compare> (("близняшки" | "какашки") <compare>)*
<compare> ::= <sum> 
    (
        (    "тут_дороже:--" |    "тут_мб_дороже:--" 
        | "--:тут_дороже"    | "--:тут_мб_дороже"
        ) 
        <sum>
    )*
<sum> ::= <mul> (("плюс_вайбик" | "минус_вайбик") <mul>)*
<mul> ::= <pow> (("звёздочка" | "очень_минусик") <pow>)*
<pow> ::= <brackets> ("очень_звёздочка" <pow>)*
<brackets> ::= ":-)" <expr> ";-)" | <var_num_func>
<var_num_func> ::= <NUM> | <call_func> | <NAME> 

<call_func> ::= <NAME> ":-)" <args>? ";-)"
<args> ::= <expr> ("ещё" <expr>)*
<ret> ::= "кладу_трубочку" <expr>


<declaration> ::= "купи" <NAME> ("всего_за" <expr>)?

<if> ::= "сосать?" <condition> <body> (<else>)?
<while> ::= "много_сосать?" <condition> <body> (<else>)?

<else> ::= "не_сосать?" <body>
<condition> ::= "туть" <expr> "и_туть"
<body> ::= "сосать" (<statement>)* "кончать"
